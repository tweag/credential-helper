load("@bazel_binaries//:defs.bzl", "bazel_binaries")
load(
    "@rules_bazel_integration_test//bazel_integration_test:defs.bzl",
    "bazel_integration_tests",
    "integration_test_utils",
)

# gazelle:exclude customized
# gazelle:exclude full

bazel_integration_tests(
    name = "full_test",
    additional_env_inherit = [
        "AWS_ACCESS_KEY_ID",
        "AWS_DEFAULT_REGION",
        "AWS_REGION",
        "AWS_SECRET_ACCESS_KEY",
        "AWS_SESSION_TOKEN",
        "GH_TOKEN",
        "GHCR_TOKEN",
        "GITHUB_TOKEN",
        "GOOGLE_APPLICATION_CREDENTIALS",
        "R2_ACCESS_KEY_ID",
        "R2_SECRET_ACCESS_KEY",
    ],
    bazel_versions = bazel_binaries.versions.all,
    env = {"CREDENTIAL_HELPER_LOGGING": "debug"},
    test_runner = "//bzl/private/integration_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("full") +
                      integration_test_utils.glob_workspace_files("testing") + [
        "//bzl/private/source_files:release_src_files",
    ],
    workspace_path = "full",
)

bazel_integration_tests(
    name = "customized_test",
    bazel_versions = bazel_binaries.versions.all,
    # This integration test does not use real credentials.
    # It is safe to enable debug logging by default.
    env = {
        "CREDENTIAL_HELPER_LOGGING": "debug",
        "BAZEL_INTEGRATION_TEST_INSTALL_TARGET": "//:custom_installer",
    },
    test_runner = "//bzl/private/integration_test_runner",
    workspace_files = integration_test_utils.glob_workspace_files("customized") +
                      integration_test_utils.glob_workspace_files("testing") + [
        "//bzl/private/source_files:release_src_files",
    ],
    workspace_path = "customized",
)

test_suite(
    name = "integration_tests",
    tags = integration_test_utils.DEFAULT_INTEGRATION_TEST_TAGS,
    tests = integration_test_utils.bazel_integration_test_names(
        "customized_test",
        bazel_binaries.versions.all,
    ) + integration_test_utils.bazel_integration_test_names(
        "full_test",
        bazel_binaries.versions.all,
    ),
    visibility = ["//:__subpackages__"],
)

filegroup(
    name = "all_files",
    srcs = glob(["*"]),
    visibility = ["//:__subpackages__"],
)
